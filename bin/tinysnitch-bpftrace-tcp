#!/usr/bin/env bpftrace
/*
 * based on: https://github.com/iovisor/bpftrace/blob/182923e64c0bad8784ff38cdeae2040f90172676/tools/tcpconnect.bt
 * Copyright (c) 2018 Dale Hamel.
 * Licensed under the Apache License, Version 2.0 (the "License")
 *
 */

#include <linux/socket.h>
#include <net/sock.h>

kprobe:tcp_connect
{
  $sk = ((struct sock *) arg0);
  $inet_family = $sk->__sk_common.skc_family;
  if ($inet_family == AF_INET) {
    $daddr = $sk->__sk_common.skc_daddr;
    $saddr = $sk->__sk_common.skc_rcv_saddr;
    $sport = $sk->__sk_common.skc_num;
    $dport = $sk->__sk_common.skc_dport;
    $dport = ($dport >> 8) | (($dport << 8) & 0x00FF00); // Destination port is big endian, it must be flipped
    printf("%d %s %d %s %d\n", pid, ntop(AF_INET, $saddr), $sport, ntop(AF_INET, $daddr), $dport);
  }
}
